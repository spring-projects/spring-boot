plugins {
	id "war"
	id "org.springframework.boot.conventions"
	id "org.springframework.boot.system-test"
}

description = "Spring Boot Deployment Tests"

configurations {
	providedRuntime {
		extendsFrom dependencyManagement
	}
}

configurations.all {
	exclude module: "spring-boot-starter-logging"
}

dependencies {
	implementation(project(":spring-boot-project:spring-boot-starters:spring-boot-starter-web")) {
		exclude group: "org.hibernate.validator"
	}
	implementation(project(":spring-boot-project:spring-boot-starters:spring-boot-starter-actuator"))

	systemTestImplementation(enforcedPlatform(project(path: ":spring-boot-project:spring-boot-parent")))
	systemTestImplementation(project(":spring-boot-project:spring-boot-starters:spring-boot-starter-test"))
	systemTestImplementation(project(":spring-boot-project:spring-boot-tools:spring-boot-test-support"))
	systemTestImplementation("org.apache.httpcomponents:httpasyncclient") {
		exclude group: "commons-logging", module: "commons-logging"
	}
	systemTestImplementation("org.awaitility:awaitility")
	systemTestImplementation("org.testcontainers:junit-jupiter")
	systemTestImplementation("org.testcontainers:testcontainers")
	systemTestImplementation("org.springframework:spring-web")

	providedRuntime(project(":spring-boot-project:spring-boot-starters:spring-boot-starter-tomcat"))
}

systemTest {
	inputs.files(war).withNormalizer(ClasspathNormalizer).withPropertyName("war")
}

war {
	archiveVersion = ''
}