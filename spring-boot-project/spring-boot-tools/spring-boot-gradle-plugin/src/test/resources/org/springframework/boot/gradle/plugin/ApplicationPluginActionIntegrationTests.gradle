buildscript {
	dependencies {
		classpath files(pluginClasspath.split(','))
	}
}

apply plugin: 'org.springframework.boot'

if (project.hasProperty('applyApplicationPlugin')) {
	apply plugin: 'application'
	applicationDefaultJvmArgs = ['-Dcom.example.a=alpha', '-Dcom.example.b=bravo']
}

task('taskExists') {
	doFirst {
		println "$taskName exists = ${tasks.findByName(taskName) != null}"
	}
}

task('distributionExists') {
	doFirst {
		boolean found = project.hasProperty('distributions') &&
				distributions.findByName(distributionName) != null
		println "$distributionName exists = $found"
	}
}

task('javaCompileEncoding') {
	doFirst {
		tasks.withType(JavaCompile) {
			println "$name = ${options.encoding}"
		}
	}
}

task('startScriptsDefaultJvmOpts') {
	doFirst {
		tasks.withType(org.springframework.boot.gradle.tasks.application.CreateBootStartScripts) {
			println "$name defaultJvmOpts = $defaultJvmOpts"
		}
	}
}
